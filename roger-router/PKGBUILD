# Maintainer: Philipp Wolfer <ph.wolfer@gmail.com>
# Contributor: Jens Rudolf <jens.rudolf@gmx.net>

_name=rogerrouter
pkgname=roger-router
pkgver=2.1.5
pkgrel=1
pkgdesc="Journal, Fax-Software and Call-Monitor for AVM FRITZ!Box or compatible"
arch=('x86_64')
url="https://www.tabos.org/"
license=('GPL2')

depends=(gtk3 libsoup ghostscript librm poppler-glib)
makedepends=(meson ninja)
optdepends=(
    'cups: FAX printer'
    'evolution-data-server: Evolution address book support'
    'libappindicator-gtk3: App indicator support'
    'libgdata: Google address book support'
)

conflicts=("${_name}" "roger")
provides=("${_name}=${pkgver}" "roger=${pkgver}")
options=('!emptydirs')
install=roger-router.install
source=("https://gitlab.com/tabos/rogerrouter/-/archive/v${pkgver}/${_name}-v${pkgver}.tar.gz")
sha1sums=('SKIP')

build() {
    cd "${srcdir}/${_name}"
    meson --prefix /usr --buildtype=plain "builddir"
    ninja -v -C "builddir"
}

check() {
    cd "${srcdir}/${_name}"
    ninja -C "builddir" test
}

package() {
    cd "${srcdir}/${_name}"
    DESTDIR="$pkgdir" ninja -C "builddir" install
}
